/-
Copyright (c) 2025 Stefano Rocca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Stefano Rocca
-/
import Mathlib.MeasureTheory.Group.Defs
import Mathlib.MeasureTheory.Group.Action
import Mathlib.MeasureTheory.Measure.Typeclasses.Finite

/-!
# FÃ¸lner filters - definitions and properties

This file defines FÃ¸lner filters for measurable spaces acted on by a group.

## Definitions

* `IsFoelner G Î¼ l` : A FÃ¸lner filter on a measurable `G`-space `X` with measure `Î¼`
  (a measurable space with an action of `G`) is a filter `l` on `Set X` such that:
    1. Each `s in l` is eventually measurable with finite non-zero measure,
    2. For all `g : G`, `Î¼ (g â€¢ s âˆ† s) / Î¼ (s)` tends to `0`.

* `MaximalFoelner G Î¼` : The maximal FÃ¸lner filter for a measurable `G`-space `X`
  with measure `Î¼` is the pullback of `ğ“ 0` along the map `s â†¦ Î¼ (g â€¢ s) / Î¼ s` on measurable
  sets of finite non-zero measure.

-/

open MeasureTheory Filter Set
open scoped ENNReal Pointwise symmDiff Topology Filter

variable {G X : Type*} [MeasurableSpace X] [Group G] [MulAction G X] {Î¼ : Measure X}

namespace Filter

variable (G Î¼) in
/-- A FÃ¸lner filter on a measurable `G`-space `X` with measure `Î¼`
(a measurable space with an action of `G`) is a filter `l` on `Set X` such that:
    1. Each `s in l` is eventually measurable with finite non-zero measure,
    2. For all `g : G`, `Î¼ (g â€¢ s âˆ† s) / Î¼ (s)` tends to `0`. -/
structure IsFoelner (l : Filter (Set X)) : Prop where
  meas_set : âˆ€á¶  s in l, MeasurableSet s
  meas_ne_zero : âˆ€á¶  s in l, Î¼ s â‰  0
  meas_ne_top : âˆ€á¶  s in l, Î¼ s â‰  âˆ
  tendsto_meas_symmDiff (g : G) : Tendsto (fun s â†¦ Î¼ ((g â€¢ s) âˆ† s) / Î¼ s) l (ğ“ 0)

/-- The constant filter `X` is FÃ¸lner if `X` has finite measure. -/
lemma IsFoelner.pure_of_isFiniteMeasure [NeZero Î¼] [IsFiniteMeasure Î¼] :
    IsFoelner G Î¼ (pure .univ) where
  meas_set := by simp
  meas_ne_zero := by simp [NeZero.ne]
  meas_ne_top := by simp
  tendsto_meas_symmDiff (g : G) := by
    simpa using tendsto_pure_nhds (fun s â†¦ Î¼ ((g â€¢ s) âˆ† s) / Î¼ s) .univ

/-- If there exists a non-trivial FÃ¸lner filter on the measurable `G`-space `X`,
then it exists a `G`-invariant finitely additive probability measure on `X`. -/
lemma IsFoelner.amenable [SMulInvariantMeasure G X Î¼] {l : Filter (Set X)} [NeBot l]
    (hfoel : IsFoelner G Î¼ l) : âˆƒ m : Set X â†’ â„â‰¥0âˆ, m .univ = 1 âˆ§
    (âˆ€ s t, MeasurableSet t â†’ Disjoint s t â†’ m (s âˆª t) = m s + m t) âˆ§
    âˆ€ (g : G) (s : Set X), m (g â€¢ s) = m s := by
  set u := Ultrafilter.of l
  set m := fun t â†¦ limUnder u (fun s â†¦ Î¼ (t âˆ© s) / Î¼ s)
  have compact_Icc := @isCompact_Icc â„â‰¥0âˆ _ _ _ 0 1
  have subset_Icc : âˆ€ t, âˆ€á¶  s in u, Î¼ (t âˆ© s) / Î¼ s âˆˆ Icc 0 1 := fun t â†¦
    Eventually.mono (
      (Eventually.filter_mono (Ultrafilter.of_le l) hfoel.meas_ne_zero).and
      (Eventually.filter_mono (Ultrafilter.of_le l) hfoel.meas_ne_top))
    (fun i hi â†¦ by simp [ENNReal.div_le_iff (hi.1) (hi.2)]; exact Î¼.mono (inter_subset_right))
  use m
  refine âŸ¨?_, ?_, ?_âŸ©
  Â· refine limUnder_mono (tendsto_congr' ?_|>.mp tendsto_const_nhds) (Ultrafilter.of_le l)
    exact Eventually.mono
      (hfoel.meas_ne_zero.and hfoel.meas_ne_top)
      (fun _ hi â†¦ by simp [ENNReal.div_self (hi.1) (hi.2)])
  Â· intro s t ht hdisj
    obtain âŸ¨_, _, hâ‚âŸ© := u.tendsto_of_eventually_mem_compact compact_Icc (subset_Icc s)
    obtain âŸ¨_, _, hâ‚‚âŸ© := u.tendsto_of_eventually_mem_compact compact_Icc (subset_Icc t)
    simp[m, â† limUnder_add hâ‚ hâ‚‚, union_inter_distrib_right, â† ENNReal.add_div]
    refine limUnder_congr' (Eventually.mono
      (Eventually.filter_mono (Ultrafilter.of_le l) hfoel.meas_set)
      (fun i hi â†¦ ?_))
    simp[measure_union
      (Disjoint.mono (inter_subset_left) (inter_subset_left) hdisj)
      (MeasurableSet.inter ht hi)]
  Â· intro g t
    suffices h_le : âˆ€ (h h' : G), m (h â€¢ t) â‰¤ m (h' â€¢ t) by
      simpa [one_smul] using le_antisymm (h_le g 1) (h_le 1 g)
    intro h h'
    have tendstoâ‚€ : Tendsto (fun s â†¦ Î¼ ((hâ»Â¹ â€¢ s) âˆ† (h'â»Â¹ â€¢ s)) / Î¼ s) u (ğ“ 0) := by
      simpa [u, â† smul_smul, measure_smul_symmDiff _ h'] using
        Tendsto.mono_left (hfoel.tendsto_meas_symmDiff (h' * hâ»Â¹)) (Ultrafilter.of_le l)
    have h_le : âˆ€ s, Î¼ (h â€¢ t âˆ© s) â‰¤ Î¼ (h' â€¢ t âˆ© s) + Î¼ ((hâ»Â¹ â€¢ s) âˆ† (h'â»Â¹ â€¢ s)) := by
      intro s
      simp_all [measure_smul_inter]
      set A := t âˆ© hâ»Â¹ â€¢ s
      set B := t âˆ© h'â»Â¹ â€¢ s
      calc
        Î¼ A â‰¤ Î¼ B + Î¼ (A \ B) := by simpa [Set.inter_union_diff] using
          (measure_union_le (A âˆ© B) (A \ B)).trans <| add_le_add_right (measure_mono (by simp)) _
        _ â‰¤ Î¼ B + Î¼ ((hâ»Â¹ â€¢ s) âˆ† (h'â»Â¹ â€¢ s)) :=
          add_le_add_left (by
            rw [â† inter_diff_distrib_left]
            apply measure_mono
            exact inter_subset_right.trans (by simp[symmDiff_def])) _
    have := fun s â†¦ (by simpa [ENNReal.add_div] using ENNReal.div_le_div_right (h_le s) (Î¼ s))
    obtain âŸ¨_, _, hâ‚âŸ© := u.tendsto_of_eventually_mem_compact compact_Icc (subset_Icc (h â€¢ t))
    obtain âŸ¨_, _, hâ‚‚âŸ© := u.tendsto_of_eventually_mem_compact compact_Icc (subset_Icc (h' â€¢ t))
    simpa [m, limUnder_add hâ‚‚ tendstoâ‚€, Tendsto.limUnder_eq tendstoâ‚€] using
      limUnder_le_of_tendsto_of_tendsto' hâ‚ (Tendsto.add hâ‚‚ tendstoâ‚€) this

variable (G Î¼) in
/-- The maximal FÃ¸lner filter for the measurable `G`-space `X` with measure `Î¼` is the pullback
of `ğ“ 0` along the map `S â†¦ Î¼ (g â€¢ S) / Î¼ S` on measurable sets of finite non-zero measure. -/
def MaximalFoelner : Filter (Set X) :=
  ğ“Ÿ {s : Set X | MeasurableSet s âˆ§ Î¼ s â‰  0 âˆ§ Î¼ s â‰  âˆ} âŠ“
  â¨… (g : G), (comap (fun s => Î¼ ((g â€¢ s) âˆ† s) / Î¼ s) (ğ“ 0))

theorem isFoelner_iff_le (l : Filter (Set X)) :
    IsFoelner G Î¼ l â†” l â‰¤ MaximalFoelner G Î¼ := by
  simp_all [MaximalFoelner, â† eventually_iff, eventually_and]
  constructor
  all_goals intro h
  Â· exact âŸ¨âŸ¨h.meas_set, h.meas_ne_zero, h.meas_ne_topâŸ©,
      fun g â†¦ tendsto_iff_comap.1 (h.tendsto_meas_symmDiff g)âŸ©
  Â· exact âŸ¨h.1.1, h.1.2.1, h.1.2.2, fun g â†¦ tendsto_iff_comap.2 (h.2 g)âŸ©

theorem isFoelner_map_iff_tendsto {Î¹ : Type*} (l : Filter Î¹) (F : Î¹ â†’ Set X) :
    IsFoelner G Î¼ (l.map F) â†” Tendsto F l (MaximalFoelner G Î¼) := isFoelner_iff_le (l.map F)

theorem amenable_of_maximalFoelner_ne_bot
    [SMulInvariantMeasure G X Î¼] (h : NeBot (MaximalFoelner G Î¼)) :
    âˆƒ m : Set X â†’ â„â‰¥0âˆ, m .univ = 1 âˆ§
    (âˆ€ s t, MeasurableSet t â†’ Disjoint s t â†’ m (s âˆª t) = m s + m t) âˆ§
    âˆ€ (g : G) (s : Set X), m (g â€¢ s) = m s :=
  IsFoelner.amenable <|
    (isFoelner_map_iff_tendsto _ _).2 <| @tendsto_id _ (MaximalFoelner G Î¼)

end Filter
